#https://bnfplayground.pauliankline.com/

<bop> ::= "|" | "&" | "^"
<id> ::= [a-z] ([a-z] | [0-9])*
<float> ::= "-" [0-9]+ "." [0-9]+
	| [0-9]+ "." [0-9]+
<int> ::= "-" [0-9]+ 
	| [0-9]+
<num> ::= <float> | <int>

<str> ::= "\"\""
    | "\"" ([0-9] | [a-z] | " ")+ "\""

<cmd> ::= "#" <id>
<cmdExpr> ::= <cmdExpr> <cmd>
    | <cmd>

<dirOp> ::= ":" | "=" | "~"
<dirNumbOp> ::= ("<=" | "<" | ">" | ">=") <num>
<dirRangeOp> ::= "[" <num> ".." <num> "]"
	| "[" <num> "..]"
    | "[.." <num> "]"
<dir> ::= <id> <dirOp> (<str> | <num>)
    | <id> <dirNumbOp>
    | <id> <dirRangeOp>

<logicExpr> ::= "(" <logicExpr> ")"
	| "!" <logicExpr>
	| <logicExpr> <bop> <logicExpr>
    | <ruleExpr>

<ruleExpr> ::= "(" <ruleExpr> ")"
    | "!" <ruleExpr>
    | <dir>

<strexp> ::= "\"\""
    | "\"" ([0-9] | [a-z] | " ")+ "\""
    | ([0-9] | [a-z] | " ")+

<query> ::= <cmdExpr> <query>
    | <strexp>
    | <logicExpr>