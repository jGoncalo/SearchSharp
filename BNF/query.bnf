#https://bnfplayground.pauliankline.com/

<bool> ::= "True" | "False"
<bop> ::= "|" | "&" | "^"
<id> ::= [a-z] ([a-z] | [0-9])*
<float> ::= "-" [0-9]+ "." [0-9]+
	| [0-9]+ "." [0-9]+
<int> ::= "-" [0-9]+ 
	| [0-9]+
<num> ::= <float> | <int>

<str> ::= "\"\""
    | "\"" ([0-9] | [a-z] | " ")+ "\""

<lit> ::= <num> | <str> | <bool>
<args> ::= <args> "," <lit>
	| <lit>

<numList> ::= <numList> "," <num>
    | <num>
<strList> ::= <strList> "," <str>
    | <str>
<boolList> ::= <boolList> "," <bool>
    | <bool>

<cmd> ::= "#" <id> "(" <args> ")"
    | "#" <id> "()"
	| "#" <id>
<cmdExpr> ::= <cmdExpr> <cmd>
    | <cmd>

<dirOp> ::= ":" | "=" | "~"
<dirNumbOp> ::= ("<=" | "<" | ">" | ">=") <num>
<dirRangeOp> ::= "[" <num> ".." <num> "]"
	| "[" <num> "..]"
    | "[.." <num> "]"
<dirListOp> ::= "[" (<numList> | <strList> | <boolList>) "]"
<dir> ::= <id> <dirOp> (<str> | <num> | <bool>)
    | <id> <dirNumbOp>
    | <id> <dirRangeOp>
    | <id> <dirListOp>

<logicExpr> ::= "(" <logicExpr> ")"
	| "!" <logicExpr>
	| <logicExpr> <bop> <logicExpr>
    | <ruleExpr>

<ruleExpr> ::= "(" <ruleExpr> ")"
    | "!" <ruleExpr>
    | <dir>
    | <str>

<strexp> ::= "\"\""
    | "\"" ([0-9] | [a-z] | " ")+ "\""
    | ([0-9] | [a-z] | " ")+

<prv> ::= "<" <id> "@" <id> ">"
    | "<@" <id> ">"
    | "<" <id> ">"

<constraint> ::= <strexp>
    | <logicExpr>

<pQuery> ::= <prv> <cmdExpr>
    | <prv>
    | <cmdExpr>

<query> ::= <pQuery> <constraint>
    | <pQuery>
    | <constraint>