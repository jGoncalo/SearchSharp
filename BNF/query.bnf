#https://bnfplayground.pauliankline.com/

<bool> ::= "True" | "False"
<bop> ::= "|" | "&" | "^"
<id> ::= [a-z] ([a-z] | [0-9])*
<float> ::= "-" [0-9]+ "." [0-9]+
	| [0-9]+ "." [0-9]+
<int> ::= "-" [0-9]+ 
	| [0-9]+
<num> ::= <float> | <int>

<str> ::= "\"\""
    | "\"" ([0-9] | [a-z] | " ")+ "\""

<arg> ::= <num> | <str> | <bool>
<args> ::= <args> "," <arg>
	| <arg>

<cmd> ::= "#" <id> "(" <args> ")"
	| "#" <id>
<cmdExpr> ::= <cmdExpr> <cmd>
    | <cmd>

<dirOp> ::= ":" | "=" | "~"
<dirNumbOp> ::= ("<=" | "<" | ">" | ">=") <num>
<dirRangeOp> ::= "[" <num> ".." <num> "]"
	| "[" <num> "..]"
    | "[.." <num> "]"
<dir> ::= <id> <dirOp> (<str> | <num> | <bool>)
    | <id> <dirNumbOp>
    | <id> <dirRangeOp>

<logicExpr> ::= "(" <logicExpr> ")"
	| "!" <logicExpr>
	| <logicExpr> <bop> <logicExpr>
    | <ruleExpr>

<ruleExpr> ::= "(" <ruleExpr> ")"
    | "!" <ruleExpr>
    | <dir>
    | <str>

<strexp> ::= "\"\""
    | "\"" ([0-9] | [a-z] | " ")+ "\""
    | ([0-9] | [a-z] | " ")+

<prv> ::= "<" <id> ">"

<query> ::= <prv> <query>
    | <cmdExpr> <query>
    | <strexp>
    | <logicExpr>